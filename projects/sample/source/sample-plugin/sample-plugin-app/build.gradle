apply from: "../sample-normal-app/build.gradle"

android {
    defaultConfig {
        applicationId project.SAMPLE_HOST_APP_APPLICATION_ID
    }
}

dependencies {
    //Shadow Transform后业务代码会有一部分实际引用runtime中的类
    //如果不以compileOnly方式依赖，会导致其他Transform或者Proguard找不到这些类
    compileOnly 'com.tencent.shadow.core:runtime-debug'
}

buildscript {
    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath 'com.tencent.shadow.core:runtime-debug'
        classpath 'com.tencent.shadow.core:activity-container-debug'
        classpath 'com.tencent.shadow.core:gradle-plugin'
        classpath 'org.javassist:javassist:3.22.0-GA'
    }
}

def createDuplicateApkTask(buildType) {
    def apkDir = file("${getBuildDir()}/outputs/apk/$buildType")

    return tasks.create("duplicate${buildType.capitalize()}ApkTask", Copy) {
        group = 'build'
        description = "复制一个sample-plugin-app-${buildType}.apk用于测试目的"
        from(apkDir) {
            include("sample-plugin-app-${buildType}.apk")
            rename { "sample-plugin-app-${buildType}2.apk" }
        }
        into(apkDir)

    }.dependsOn(":sample-plugin-app:assemble${buildType.capitalize()}")
}

tasks.whenTaskAdded { task ->
    if (task.name == "assembleDebug") {
        def createTask = createDuplicateApkTask('debug')
        task.finalizedBy(createTask)
    }
    if (task.name == "assembleRelease") {
        def createTask = createDuplicateApkTask('release')
        task.finalizedBy(createTask)
    }
}

apply plugin: 'com.tencent.shadow.plugin'


shadow {
    transform {
//        useHostContext = ['abc']
    }

    packagePlugin {
        pluginTypes {
            debug {
                loaderApkConfig = new Tuple2('sample-loader-debug.apk', ':sample-loader:assembleDebug')
                runtimeApkConfig = new Tuple2('sample-runtime-debug.apk', ':sample-runtime:assembleDebug')
                pluginApks {
                    pluginApk1 {
                        businessName = 'sample-plugin-app'
                        partKey = 'sample-plugin-app'
                        buildTask = ':sample-plugin-app:assembleDebug'
                        apkName = 'sample-plugin-app-debug.apk'
                        apkPath = 'projects/sample/source/sample-plugin/sample-plugin-app/build/outputs/apk/debug/sample-plugin-app-debug.apk'
                        hostWhiteList = ["com.tencent.shadow.sample.host.lib"]
                    }
                    pluginApk2 {
                        businessName = 'sample-plugin-app2'
                        partKey = 'sample-plugin-app2'
                        buildTask = ':sample-plugin-app:assembleDebug'
                        apkName = 'sample-plugin-app-debug2.apk'
                        apkPath = 'projects/sample/source/sample-plugin/sample-plugin-app/build/outputs/apk/debug/sample-plugin-app-debug2.apk'
                        hostWhiteList = ["com.tencent.shadow.sample.host.lib"]
                    }
                }
            }

            release {
                loaderApkConfig = new Tuple2('sample-loader-release.apk', ':sample-loader:assembleRelease')
                runtimeApkConfig = new Tuple2('sample-runtime-release.apk', ':sample-runtime:assembleRelease')
                pluginApks {
                    pluginApk1 {
                        businessName = 'sample-plugin-app'
                        partKey = 'sample-plugin-app'
                        buildTask = ':sample-plugin-app:assembleRelease'
                        apkName = 'sample-plugin-app-release.apk'
                        apkPath = 'projects/sample/source/sample-plugin/sample-plugin-app/build/outputs/apk/release/sample-plugin-app-release.apk'
                        hostWhiteList = ["com.tencent.shadow.sample.host.lib"]
                    }
                    pluginApk2 {
                        businessName = 'sample-plugin-app2'
                        partKey = 'sample-plugin-app2'
                        buildTask = ':sample-plugin-app:assembleRelease'
                        apkName = 'sample-plugin-app-release2.apk'
                        apkPath = 'projects/sample/source/sample-plugin/sample-plugin-app/build/outputs/apk/release/sample-plugin-app-release2.apk'
                        hostWhiteList = ["com.tencent.shadow.sample.host.lib"]
                    }
                }
            }
        }

        loaderApkProjectPath = 'projects/sample/source/sample-plugin/sample-loader'
        runtimeApkProjectPath = 'projects/sample/source/sample-plugin/sample-runtime'

        archiveSuffix = System.getenv("PluginSuffix") ?: ""
        archivePrefix = 'plugin'
        destinationDir = "${getRootProject().getBuildDir()}"

        version = 4
        compactVersion = [1, 2, 3]
        uuidNickName = "1.1.5"
    }
}