apply plugin: 'com.android.library'
apply plugin: 'com.tencent.shadow.internal.aar-to-jar'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

group 'com.tencent.shadow.core'

android {
    compileSdkVersion project.COMPILE_SDK_VERSION

    defaultConfig {
        minSdkVersion project.MIN_SDK_VERSION
        targetSdkVersion project.TARGET_SDK_VERSION
        versionCode project.VERSION_CODE
        versionName project.VERSION_NAME
        buildConfigField("String","VERSION_NAME","\"${defaultConfig.versionName}\"")

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }

    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            consumerProguardFiles 'plugin-loader-release.pro'
        }

        release {
            consumerProguardFiles 'plugin-loader-release.pro'
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        main.java.srcDirs += project(':generate-delegate-code').buildDir.path + '/generated/DelegateCode/loader'
        test.java.srcDirs += 'src/test/kotlin'
    }

    // Starting in version 4.2, AGP will use the Java 8 language level by default.
    // To keep the old behavior, specify Java 7 explicitly.
    // https://developer.android.com/studio/releases/gradle-plugin?hl=lt#java-8-default
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    // For Kotlin projects, compile to Java 6 instead of 7
    kotlinOptions {
        jvmTarget = "1.6"
    }
}

dependencies {
    implementation"org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    testImplementation 'junit:junit:4.12'

    api project(':runtime')
    compileOnly project(':activity-container')
    compileOnly project(':common')
    api project(':load-parameters')
    implementation 'com.tencent.shadow.coding:lint'
}

preBuild.dependsOn(project(":generate-delegate-code").getTasksByName("generateDelegateCode", false).first())
